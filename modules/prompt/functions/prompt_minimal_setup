#
# A monochrome theme that displays basic information.
#
# Authors:
#   Brian Tse <briankftse@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/zLZNK.png
#

function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}●%f'
  fi
}

function prompt_minimal_precmd {
  vcs_info
}

function prompt_minimal_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  local home_fs
  if is-darwin; then
    if mount | awk -v "mnt=$(df ${HOME} | awk 'BEGIN {getline} {print $1}')" 'mnt == $1' | grep -qw nfs; then
      home_fs=nfs
    else
      home_fs=notnfs
    fi
  else
    home_fs=$(stat -f -L -c %T ${HOME})
  fi

  # Load required functions.
  autoload -Uz add-zsh-hook
  if [[ "${home_fs}" != "nfs" ]]; then
    autoload -Uz vcs_info

    # Add hook for calling vcs_info before each command.
    add-zsh-hook precmd prompt_minimal_precmd

    # Tell prezto we can manage this prompt
    zstyle ':prezto:module:prompt' managed 'yes'

    # Set vcs_info parameters.
    zstyle ':vcs_info:*' enable bzr git hg svn
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' stagedstr '%F{green}●%f'
    zstyle ':vcs_info:*' unstagedstr '%F{yellow}●%f'
    zstyle ':vcs_info:*' formats ' - [%b%c%u]'
    zstyle ':vcs_info:*' actionformats " - [%b%c%u|%F{cyan}%a%f]"
    zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{cyan}%r%f'
    zstyle ':vcs_info:git*+set-message:*' hooks git_status
  fi

  # Define prompts.
  if [[ "${home_fs}" != "nfs" ]]; then
    PROMPT='$? %m:%3d${vcs_info_msg_0_} » '
  else
    PROMPT='%? %m:%3d » '
  fi
  RPROMPT=''
}

function prompt_minimal_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'minimal'
}

prompt_minimal_setup "$@"
# vim: ft=zsh
